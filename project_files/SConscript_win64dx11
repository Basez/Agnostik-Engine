import os

Import('rootDir')
buildname = 'win64dx11'

env = Environment(TARGET_ARCH='x86_64', platform = 'win32', MSVC_VERSION='14.0')
Export('env')

print "Executing MSVC SConscript..."
print "rootDir: " + rootDir
print "buildname: " + buildname

#TODO:
#execute shader generation script for DX11
SConscript('../shaders/SConscript_generate_dx11_shaders')

# Compiler & Linker commands
env.AppendUnique(CXXFLAGS=Split("/EHsc"))				# specify compiler unwind semantics
env.AppendUnique(CXXFLAGS = ['/W3'])					# warning level
env.AppendUnique(CXXFLAGS = ['/WX'])					# treat warning as error
env.AppendUnique(LINKFLAGS = ['/SUBSYSTEM:WINDOWS'])
env.AppendUnique(LINKFLAGS = ['/MACHINE:X64'])
env['CCPDBFLAGS'] = '/Z7 /Fd${TARGET}.pdb'				# Generate pdb for code debugging (both release and debug) NOTE: changed to /Z7 instead of Zi because that doesnt work with precompiled headers

# specify include folder & libs folder
libPaths= ['../libs/win64']
additionalIncludes = ['../include/common', '../include/api_dx11', '../include/os_win', '../libs/win64/include', '../shaders/output_dx11']
env.Append(CPPPATH = additionalIncludes)					

#specify main entrypoint cpp
buildEntrypoint = 'main_win64dx11.cpp'

##############################################################################
### Create release env
envRelease = env.Clone()
envRelease.AppendUnique(LINKFLAGS=['/DEBUG'])								# add debug flag, comment out if final release?
envRelease.AppendUnique(CCFLAGS=['/MD'])									
envRelease.AppendUnique(CPPDEFINES = ['AGN_RELEASE']) 

# set variant dirs (temp folders)	
envRelease.VariantDir('../genfiles/gen-common-release', '../source/common', duplicate=0)		# specify intermediate folder for generated .obj files
envRelease.VariantDir('../genfiles/gen-api_dx11-release', '../source/api_dx11', duplicate=0)
envRelease.VariantDir('../genfiles/gen-os_win-release', '../source/os_win', duplicate=0)
envRelease.VariantDir('../genfiles/gen-build-release', '../source/', duplicate=0)

# Gather all cpp files
objsCommonRelease = [file for file in envRelease.Glob('../genfiles/gen-common-release/*.cpp')]
objsAPIRelease = [file for file in envRelease.Glob('../genfiles/gen-api_dx11-release/*.cpp')]
objsOSRelease = [file for file in envRelease.Glob('../genfiles/gen-os_win-release/*.cpp')]
objsBuildRelease = ['../genfiles/gen-build-release/' + buildEntrypoint]

# specify release libs
releaseLibNames=['d3d11','dxgi','d3dcompiler','winmm','assimp', 'user32', 'dxguid']

# create program
applicationProgramRelease = envRelease.Program(source = objsBuildRelease + objsCommonRelease + objsAPIRelease + objsOSRelease, target = "../build/"+buildname+"/agnostik_release", LIBS = releaseLibNames, LIBPATH = libPaths)

##############################################################################
### create debug env
envDebug = env.Clone()
		   
# debug specifics
envDebug.Append(LINKFLAGS=['/DEBUG'])
envDebug.AppendUnique(CCFLAGS=['/MDd'])													# Dynamically link with the debugging CRT
envDebug.AppendUnique(CCFLAGS=['/Od'])													# Disable optimizations
envDebug.AppendUnique(CPPDEFINES = ['AGN_DEBUG']) 

#envDebug['PDB']='${TARGET.base}.pdb'													# not sure what this does or why its required?
envDebug.VariantDir('../genfiles/gen-common-debug', '../source/common', duplicate=0)		# specify intermediate folder for generated .obj files
envDebug.VariantDir('../genfiles/gen-api_dx11-debug', '../source/api_dx11', duplicate=0)
envDebug.VariantDir('../genfiles/gen-os_win-debug', '../source/os_win', duplicate=0)
envDebug.VariantDir('../genfiles/gen-build-debug', '../source/', duplicate=0)

# Gather all cpp files
objsCommonDebug = [file for file in envDebug.Glob('../genfiles/gen-common-debug/*.cpp')]
objsAPIDebug = [file for file in envDebug.Glob('../genfiles/gen-api_dx11-debug/*.cpp')]
objsOSDebug = [file for file in envDebug.Glob('../genfiles/gen-os_win-debug/*.cpp')]
objsBuildDebug = ['../genfiles/gen-build-debug/' + buildEntrypoint]
		 
#specify debug libs
debugLibNames=['d3d11','dxgi','d3dcompiler','winmm','assimp', 'user32', 'dxguid']

# create debug program
applicationProgramDebug = envDebug.Program(source = objsBuildDebug + objsCommonDebug + objsAPIDebug + objsOSDebug, target = "../build/"+buildname+"/agnostik_debug", LIBS = debugLibNames, LIBPATH = libPaths)

##############################################################################
# generate msvs project  # MSVSProject OR MSVSSolution
project = env.MSVSProject(target = 'agnostik_' + buildname + env['MSVSPROJECTSUFFIX'],
                srcs = (Glob('../source/common/*.cpp', strings=True) +					## add all cpp files
						Glob('../source/api_dx11/*.cpp', strings=True) +
						Glob('../source/os_win/*.cpp', strings=True) +
						Glob('../source/'+ buildEntrypoint, strings=True)),
                incs = (Glob('../include/common/*.hpp', strings=True) +					## add all hpp files
						Glob('../include/api_dx11/*.hpp', strings=True) +
						Glob('../include/os_win/*.hpp', strings=True)),						
				buildtarget = applicationProgramDebug + applicationProgramRelease,
				variant = ['debug', 'release'],
				runfile = ['../build/'+buildname+'/agnostik_debug', '../build/'+buildname+'/agnostik_release'])
				
##############################################################################

Export('env')			# export var to scons main